10 _KANJI
20 DEFINT A-Z:CLEAR 8000
30 MAXFILES=2
40 OPEN"sample.txt" AS #1 LEN=255
50 OPEN"utf8sjis.txt" AS #2 LEN=4
60 FIELD #1,255 AS TX$
70 FIELD #2,2 AS KJ$
80 TL=LOF(1)
90 '----- MAIN LOOP
100 GOSUB 320
110 FOR P=1 TO L
120 CK$=MID$(TX$,P,1):CK=ASC(CK$)
130 '----- 1 byte charactor
140 IF CK<128 THEN PRINT CK$;:GOTO 260
150 '----- 2 bytes charactor
160 IF (CK AND 224)<>192 THEN 180 ELSE P=P+1:GOTO 260
170 '----- 3 bytes charactor
180 IF (CK AND 240)<>224 THEN 250 ELSE P1=(CK AND 15)*16
190 IF P=255 THEN GOSUB 320:P=0
200 P=P+1:CK=ASC(MID$(TX$,P,1)):P2=(CK AND 60)/4:P3=(CK AND 3)*64
210 IF P=255 THEN GOSUB 320:P=0
220 P=P+1:CK=ASC(MID$(TX$,P,1)):P4=(CK AND 63)
230 GET #2,(P1+P2)*256+P3+P4:PRINT KJ$;:GOTO 260
240 '----- 4 bytes charactor
250 IF (CK AND 248)=240 THEN P=P+3
260 NEXT
270 GOTO 100
280 '--------- END OF FILE
290 CLOSE #1:CLOSE #2
300 END
310 '--------- READ DATA
320 IF TL<1 THEN RETURN 290
330 GET #1:IF TL>=255 THEN L=255 ELSE L=TL
340 TL=TL-255
350 RETURN
